Reading from standard input
>,>,<<

Convert string to int
Setting limit
+++++ +
[
	Substracting until limit mem block reaches 0
	>----- ---
	>----- ---
	Decreases limit mem block
	<<-
]

Multiplying two ints
until mem block 1 = 0
>[
	Nested loop to add 1 value 
	to 3rd and 4th mem block for
	for every value in mem block 2 until = 0
	>[>+>+<<-]

	Nested loop to add 1 value
	to emptied 2nd mem block for
	for every value in mem block 3 until = 0
	>[<+>-]

	Jump back to first nested loop
	<<-
]

Seperate the digits inputed
>[-]>+> >+++++ +++++<
[
	- >- [>>>]+++++ +++++<<+
	[<<<]>>>>
]
<-
<+++++ +++++>>>[-<<<->>>]<<<

Convert int to string
Setting limit
<+++++ +
[

	>+++++ +++>
	[+++++ +++>]
	<[<]>-
]

Printing results
>>[.<<]<[<<]>>.
<++++++++++.
